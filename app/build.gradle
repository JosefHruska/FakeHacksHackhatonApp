apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'android-getlocalization'
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 16
        // "Check this issue before increasing targetSdk: https://stackoverflow.com/questions/46992843/interstitial-admob-ads-illegalstateexception-only-fullscreen-activities-can-r"
        targetSdkVersion 26
        def supportedLanguages = [ "en", "cs", "de", "fi", "it", "ru", "es", "pl", 'lt', 'vi', 'fr', 'sk'] // Deletes all other localizations. If adding new language, don't forget to update Screengrabfile as well.
        resConfigs supportedLanguages
        buildConfigField "String[]", "SUPPORTED_LANGUAGES", "{\"" + supportedLanguages.join("\",\"") + "\"}"
        vectorDrawables.useSupportLibrary = true
        if (System.getenv()["CI"]) {
            def buildNumber = System.getenv()["BUILD_NUMBER"]
            versionCode Integer.valueOf(buildNumber) + 1000 // Offset the number so we can be always higher than production number (734)
            versionName "10.0." + buildNumber // 0.5.x means alpha, production will have 10.0.x
        } else {
            versionCode 1000 // 734 Needed for Google Play testing, 1000 needed for Dynamic Link testing
            versionName "AS-BUILD" // Build from Android Studio. Just for development, it should not be distributed.
        }
        setProperty("archivesBaseName", "settle-up-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            multiDexEnabled true
        }

    }
    flavorDimensions "backend"
    productFlavors {
        sandbox {
            applicationId 'com.example.peanutbook.fakeappka'
            dimension 'backend'
        }
        live {
            applicationId 'com.example.peanutbook.fakeappka'
            dimension 'backend'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE' // Because of Jackson library
        exclude 'META-INF/rxjava.properties' // Because of rxjava
    }
}

dependencies {
    // support libraries
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:animated-vector-drawable:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:support-v13:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
    // firebase
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-auth:$firebase_version"
    implementation "com.google.firebase:firebase-database:$firebase_version"
    implementation "com.google.firebase:firebase-storage:$firebase_version"
    implementation "com.google.firebase:firebase-crash:$firebase_version"
    implementation "com.google.firebase:firebase-invites:$firebase_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_version"
    implementation "com.google.firebase:firebase-config:$firebase_version"
    implementation "com.google.firebase:firebase-ads:$firebase_version"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.4'
    implementation "com.google.android.gms:play-services-nearby:$firebase_version"
    implementation "com.google.android.gms:play-services-auth:$firebase_version"
    implementation "com.google.android.gms:play-services-ads:$firebase_version"
    // Rx
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation "io.reactivex:rxjava:1.3.3"
    implementation "com.artemzin.rxjava:proguard-rules:1.3.3.0"
    implementation 'io.reactivex:rxkotlin:1.0.0'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko-sdk15:0.10.2"
    // Apache
    implementation 'org.apache.commons:commons-lang3:3.7'
    // Our libraries
    implementation "io.stepuplabs.firebaseui.android:firebase-ui-auth:2.0.1.5"
    implementation 'io.stepuplabs.droppy:droppy:0.6.1.0'
    implementation 'com.github.step-up-labs:ViewPagerAnimator:v1.0.2'
    // Other third party libs
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.udojava:EvalEx:1.16'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.wdullaer:materialdatetimepicker:3.4.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.miguelcatalan:materialsearchview:1.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.2'
    implementation 'tyrantgit:explosionfield:1.0.1'
    implementation 'eu.inloop:pageradapter:0.1.0'
    implementation ('org.solovyev.android:checkout:1.2.1') {
        exclude group: "com.google.code.findbugs"
    }
    implementation 'com.facebook.android:facebook-android-sdk:4.22.1' // We should not update this until firebase-ui-auth is updated
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    implementation 'com.mikepenz:aboutlibraries:6.0.0'
    implementation 'com.robinhood.ticker:ticker:1.2.2'
    implementation 'net.alhazmy13.wordcloud:library:0.2.0-beta'
    implementation 'com.akexorcist:RoundCornerProgressBar:2.0.3'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'info.cukes:cucumber-java:1.2.5'
    testImplementation 'info.cukes:cucumber-junit:1.2.5'
    androidTestImplementation "com.android.support:support-annotations:$support_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.4-atlassian-1'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'tools.fastlane:screengrab:1.1.0'
    androidTestImplementation('com.schibsted.spain:barista:2.2.0') {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin'
    }
}

// Update translations by running './gradlew downloadTranslations'
getlocalization {
    if (System.getenv()["CI"]) {
        user = "" // No GetLocalization by CI
        password = ""
    } else {
        user = GETLOCALIZATION_USERNAME
        password = GETLOCALIZATION_PASSWORD
    }
    project = "settle_up"
    progress = 80
    iana_codes = [
            "cs-CZ" : "cs",
            "pl-PL" : "pl",
            "sl-SI" : "sl",
            "sk-SK" : "sk",
            "tr-TR" : "tr",
            "ca-AD" : "ca",
            "pt-BR" : "pt",
            "nl-BE" : "nl-rBE",
            "es-419": "es",
            "pt-PT" : "pt-rPT",
            "hu-HU" : "hu",
            "grk"   : "el",
            "bg-BG" : "bg",
            "no"    : "nb"
    ]
    fastlane_codes = [
            "de": "de-DE",
            "es": "es-ES",
            "fi": "fi-FI",
            "it": "it-IT",
            "ru": "ru-RU",
            "fr": "fr-FR",
            "sk-SK": "sk",
            "sv": "sv-SE"
    ]
    master = "strings.xml"
    fastlane = true
}

apply plugin: 'com.google.gms.google-services'
